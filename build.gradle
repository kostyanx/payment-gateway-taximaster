plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.10'
    id 'java-library-distribution'
    id 'idea'
}

group = 'ru.catcab'
version = '1.1.0'

repositories {
    jcenter()
}

dependencies {
    // configuration
    implementation 'com.sksamuel.hoplite:hoplite-core:1.3.13'
    implementation 'com.sksamuel.hoplite:hoplite-yaml:1.3.13'
    // common logging
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'org.apache.logging.log4j:log4j-api:2.14.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.0'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.0'
    // json logging
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.0'
    implementation 'org.apache.logging.log4j:log4j-layout-template-json:2.14.0'
    // MDC support for coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-slf4j:1.4.2'
    // inernal database
    implementation 'com.h2database:h2:1.4.200'
    implementation 'org.flywaydb:flyway-core:7.4.0'
    // dependency injections
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-logger-slf4j:$koin_version"
    // dependency injections - ktor integration
    implementation "org.koin:koin-ktor:$koin_version"
    // json kotlinx serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlin_serialization"
    // ktor http client
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-json-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_version"
    implementation "io.ktor:ktor-client-logging:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    // ktor http server
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-serialization:$ktor_version"
    //
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.12.0'
    // exposed - kotlin database framework
    implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-dao:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-java-time:$exposed_version"
    // kotlin reflect
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.21"
    // test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation "org.koin:koin-test:$koin_version"
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '13'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '13'
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.catcab.taximaster.paymentgateway.Application',
                'Class-Path': configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(' ')
    }
}

distributions {
    //noinspection GroovyAssignabilityCheck
    main {

        contents {
            from 'application.conf'
            from 'src/dist'
            from 'src/main/resources'
        }
    }
}
